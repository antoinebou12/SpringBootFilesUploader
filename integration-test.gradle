buildscript {
    repositories {
        gradlePluginPortal()
    }
    ext['junit-jupiter.version'] = '5.4.0'
}

apply plugin: 'idea'

sourceSets {
    integrationTest {
        kotlin.srcDir 'src/integrationTest/kotlin'
        compileClasspath += main.output
        runtimeClasspath += main.output
        resources.srcDir 'src/integrationTest/resources'
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.kotlin.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

compileIntegrationTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

dependencies {
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.awaitility:awaitility-kotlin:4.1.0")
    testImplementation("org.mock-server:mockserver-client-java:5.11.2")
    testImplementation("org.testcontainers:mockserver")
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

//Necessary to fix integrationTest issue with bootstrap.properties and duplicate files
tasks {
    processIntegrationTestResources {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

check.dependsOn integrationTest
